apply plugin: 'com.android.application'
apply from: "$project.rootDir/tools/script-git-version.gradle"
apply plugin: 'io.fabric'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "info.santhosh.evlo"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode gitVersionCode
        versionName gitVersionName
        vectorDrawables.useSupportLibrary = true
        resConfigs "en"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            // TODO, signing keys
            storeFile file("release.keystore")
            storePassword "myPassword"
            keyAlias "myAlias"
            keyPassword "Something...."
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix " Debug"
            buildConfigField("String", "AD_APP_ID", "\"ca-app-pub-3940256099942544~3347511713\"")
            buildConfigField("String", "AD_PUB_ID", "\"pub-3940256099942544\"")
            buildConfigField("String", "PROTO_FETCH_URL", "\"https://www.dropbox.com/s/y80ip1cj3k0lds2/commodities_test?dl=1\"")
            resValue("string", "AD_DETAIL_SCREEN_UNIT_ID", "ca-app-pub-3940256099942544/6300978111")
            resValue("string", "AD_FAVORITES_SCREEN_UNIT_ID","ca-app-pub-3940256099942544/6300978111")
        }

        release {
            buildConfigField("String", "AD_APP_ID", "\"${ad_app_id}\"")
            buildConfigField("String", "AD_PUB_ID", "\"${ad_pub_id}\"")
            buildConfigField("String", "PROTO_FETCH_URL", "\"${proto_fetch_url}\"")
            resValue("string", "AD_DETAIL_SCREEN_UNIT_ID", "${ad_detail_screen_unit_id}")
            resValue("string", "AD_FAVORITES_SCREEN_UNIT_ID", "${ad_favorites_screen_unit_id}")
            signingConfig signingConfigs.debug // while release to play store, change this
            shrinkResources true
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
}

ext {
    // version numbers
    junitVersion = '4.12'
    supportLibVersion = '28.0.0'
    constraintLayoutVersion = '1.1.3'
    firebaseCoreVersion = '16.0.1'
    firebaseAdsVersion = '15.0.1'
    okhttp3Version = '4.9.1'
    simplexmlVersion = '2.7.1'
    stethoVersion = '1.5.1'
    leakCanaryVersion = '1.6.3'
    evernoteJobVersion = '1.2.6'
    stickyHeaderVersion = '0.2.8'
    protoModelVersion = 'c7c9f95840'
    crashlyticsVersion = '2.6.8'
    recyclerViewAnimatorsVersion = '2.2.7'
    materialIntroViewVersion = '1.6.0'
    adConsentLibVersion = '1.0.7'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation "junit:junit:${junitVersion}"

    // support libs
    implementation "com.android.support:appcompat-v7:${supportLibVersion}"
    implementation "com.android.support:support-v4:${supportLibVersion}"
    implementation "com.android.support:recyclerview-v7:${supportLibVersion}"
    implementation "com.android.support:cardview-v7:${supportLibVersion}"
    implementation "com.android.support:design:${supportLibVersion}"
    implementation "com.android.support:customtabs:${supportLibVersion}"
    implementation "com.android.support.constraint:constraint-layout:${constraintLayoutVersion}"

    // firebase
    implementation "com.google.firebase:firebase-core:${firebaseCoreVersion}"
    implementation "com.google.firebase:firebase-ads:${firebaseAdsVersion}"

    // GDPR consent for admob
    implementation "com.google.android.ads.consent:consent-library:${adConsentLibVersion}"

    // job scheduler background compatibility
    implementation "com.evernote:android-job:${evernoteJobVersion}"

    // ui
    implementation "ca.barrenechea.header-decor:header-decor:${stickyHeaderVersion}"
    implementation "com.github.iammert:MaterialIntroView:${materialIntroViewVersion}"

    // okhttp
    implementation "com.squareup.okhttp3:okhttp:${okhttp3Version}"

    // for xml parsing
    implementation("org.simpleframework:simple-xml:${simplexmlVersion}") {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }

    // protobuf model and protobuf lite dependency
    implementation "com.github.santhoshvai:evlo-protobuf-model:${protoModelVersion}"

    // crash reporting
    implementation("com.crashlytics.sdk.android:crashlytics:${crashlyticsVersion}@aar") {
        transitive = true
    }

    // leak canary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:${leakCanaryVersion}"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:${leakCanaryVersion}"
    debugImplementation "com.squareup.leakcanary:leakcanary-support-fragment:${leakCanaryVersion}"

    // stetho - for inspecting networks and sqlite dbs
    debugImplementation "com.facebook.stetho:stetho:${stethoVersion}"
    implementation "jp.wasabeef:recyclerview-animators:${recyclerViewAnimatorsVersion}"
}

apply plugin: 'com.google.gms.google-services'
